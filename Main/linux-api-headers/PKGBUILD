# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=linux-api-headers
pkgver=4.12
pkgrel=1
pkgdesc="The Linux API Headers expose the kernel's API for use by Glibc."
arch=('i686' 'x86_64')
url="http://www.kernel.org/"
groups=('core')
license=('GPL')
makedepends=('bash'
             'coreutils'
             'diffutils'
             'findutils'
             'glibc'
             'grep'
             'gzip'
             'kmod'
             'make'
             'ncurses'
             'perl'
             'sed')
depends=('rootfs')
source=("https://www.kernel.org/pub/linux/kernel/v4.x/linux-${pkgver}.tar.xz")

build(){
  cd ${srcdir}/linux-${pkgver}
  make mrproper
  make INSTALL_HDR_PATH=dest headers_install
  find dest/include \( -name .install -o -name ..install.cmd \) -delete
}

package(){
  install -vdm755 ${pkgdir}/usr/include/
  cd ${srcdir}/linux-${pkgver}
  cp -rv dest/include/* ${pkgdir}/usr/include
}

sha256sums=('a45c3becd4d08ce411c14628a949d08e2433d8cdeca92036c7013980e93858ab')
