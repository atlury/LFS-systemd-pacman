# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=openssl
_ver=1.0.2j
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('i686' 'x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
groups=('core')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gcc'
             'glibc'
             'make'
             'sed')
depends=('zlib'
         'perl'
         $(pacman -Q krb5 > /dev/null 2>&1 && echo 'krb5'))
optdepends=('ca-certificates: provides certificate authority store'
            'krb5: add GSSAPI support')
options=('!makeflags')
backup=('etc/ssl/openssl.cnf')
_confargs=""
_krb5="0"
source=("https://www.${pkgname}.org/source/${pkgname}-${_ver}.tar.gz")

build() {
  # Handle optional deps
  cd "${pkgname}-${_ver}"
  ./config --prefix=/usr         \
           --openssldir=/etc/ssl \
           --libdir=lib          \
           shared                \
           zlib-dynamic
  make depend
  make
  sed -i 's# libcrypto.a##;s# libssl.a##' Makefile
}

check() {
  cd "${pkgname}-$_{ver}"
  make -j1 test
}

package() {
  cd "${pkgname}-${_ver}"
  make INSTALL_PREFIX="${pkgdir}" MANDIR=/usr/share/man MANSUFFIX=ssl install
  install -vdm755 "${pkgdir}/usr/share/doc/${pkgname}-${_ver}"
  cp -vfr doc/* "${pkgdir}/usr/share/doc/${pkgname}-${_ver}"
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

sha256sums=('e7aff292be21c259c6af26469c7a9b3ba26e9abaaffd325e3dccc9785256c431')

