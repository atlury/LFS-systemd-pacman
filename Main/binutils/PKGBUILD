# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=binutils
pkgver=2.28
pkgrel=1
pkgdesc="The Binutils package contains a linker, an assembler, and other tools for handling object files."
arch=('x86_64')
url="http://www.gnu.org/software/${pkgname}/"
license=('GPL')
groups=('core')
makedepends=('bash'
             'binutils'
             'coreutils'
             'diffutils'
             'file'
             'gawk'
             'gcc'
             'grep'
             'make'
             'perl'
             'sed'
             'texinfo')
depends=('glibc'
         'zlib')
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2")

prepare(){
  cd ${srcdir}/${pkgname}-${pkgver}
  # Kill $CPP and $CPPFLAGS when testing header files (Thanks Arch Linux)
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
  mkdir -v build
}

build(){ 
  cd ${srcdir}/${pkgname}-${pkgver}/build
  ../configure --prefix=/usr       \
               --enable-gold       \
               --enable-ld=default \
               --enable-plugins    \
               --enable-shared     \
               --disable-werror    \
               --with-system-zlib
  # Make sure build environment is sane before wasting time
  make configure-host

  make tooldir=/usr
}

check(){
  cd ${srcdir}/${pkgname}-${pkgver}/build
  make LDFLAGS="" -k check 2>&1 | tee test.log || true
  # one test fails and four error with new kenerl headers
  failures=`grep "tests failed" test.log | cut -d " " -f 1`
  if test "${failures}" != "1"; then
    echo "Unexpected test failures..." && exit 1
  fi
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}/build
  make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install
}
sha256sums=('6297433ee120b11b4b0a1c8f3512d7d73501753142ab9e2daa13c5a3edd32a72')
