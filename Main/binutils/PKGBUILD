# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=('binutils-debug'
         'binutils')
pkgbase=binutils
pkgver=2.29
pkgrel=1
arch=('x86_64')
url="http://www.gnu.org/software/${pkgbase}/"
license=('GPL')
groups=('core')
makedepends=('bash'
             'binutils'
             'coreutils'
             'diffutils'
             'file'
             'gawk'
             'gcc'
             'grep'
             'make'
             'perl'
             'sed'
             'texinfo')
source=("http://ftp.gnu.org/gnu/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2")

prepare(){
  cd ${srcdir}/${pkgbase}-${pkgver}
  # Kill $CPP and $CPPFLAGS when testing header files (Thanks Arch Linux)
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
  mkdir -v build
}

build(){ 
  cd ${srcdir}/${pkgbase}-${pkgver}/build
  ../configure --prefix=/usr       \
               --enable-gold       \
               --enable-ld=default \
               --enable-plugins    \
               --enable-shared     \
               --disable-werror    \
               --with-system-zlib
  # Make sure build environment is sane before wasting time
  make configure-host

  make tooldir=/usr
}

check(){
  cd ${srcdir}/${pkgbase}-${pkgver}/build
  make LDFLAGS="" -k check 2>&1 | tee test.log || true
  # one test fails and four error with new kenerl headers
  failures=`grep "tests failed" test.log | cut -d " " -f 1`
  if test "${failures}" != "1"; then
    echo "Unexpected test failures..." && exit 1
  fi
}

package_binutils-debug(){
  pkgdesc="Debugging symbols for buinutils."
  depends=("binutils=${pkgver}-${pkgrel}")
  options=(!strip)

  # Install to staging location first...
  cd ${srcdir}/${pkgbase}-${pkgver}/build
  make prefix=${srcdir}/binutils-dest/usr \
       tooldir=${srcdir}/binutils-dest/usr \
       install

  install -vdm755 "${pkgdir}/usr/lib"
  for _lib in "${srcdir}"/binutils-dest/usr/lib/*.so
  do
    _libbase=$(basename "${_lib}")
    objcopy --only-keep-debug "${_lib}" "${pkgdir}/usr/lib/${_libbase}.dbg"
  done
}

package_binutils(){
pkgdesc="The Binutils package contains a linker, an assembler, and other tools for handling object files."
depends=('glibc'
         'zlib')

  cp -R ${srcdir}/binutils-dest/* ${pkgdir}/
}
sha256sums=('29a29549869039aad75fdf507ac30366da5ad0b974fbff4a8e7148dbf4f40ebf')
