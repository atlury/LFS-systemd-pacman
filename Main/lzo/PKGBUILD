# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=lzo
pkgver=2.09
pkgrel=1
pkgdesc="Portable lossless data compression library"
arch=('i686' 'x86_64')
url="http://www.oberhumer.com/opensource/lzo"
license=('GPL')
depends=('glibc')
source=("http://www.oberhumer.com/opensource/${pkgname}/download/${pkgname}-${pkgver}.tar.gz")

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr    \
              --enable-shared  \
              --disable-static \
              --docdir=/usr/share/doc/lzo-2.09
  make
  gcc ${CFLAGS} -fpic -Iinclude/lzo -o minilzo/minilzo.o -c minilzo/minilzo.c
  gcc ${LDFLAGS} -shared -o libminilzo.so.0 -Wl,-soname,libminilzo.so.0 \
                              minilzo/minilzo.o
}

check() {
  cd "${pkgname}-${pkgver}"
  make test
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vm755 libminilzo.so.0 "${pkgdir}/usr/lib"
  install -pvm644 minilzo/minilzo.h "${pkgdir}/usr/include/lzo"
  cd "${pkgdir}/usr/lib"
  ln -s libminilzo.so.0 libminilzo.so
}

sha256sums=('f294a7ced313063c057c504257f437c8335c41bfeed23531ee4e6a2b87bcb34c')

