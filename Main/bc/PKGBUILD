# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=bc
pkgver=1.06.95
pkgrel=1
pkgdesc="The Bc package contains an arbitrary precision numeric processing language."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/${pkgname}/"
license=('GPL')
groups=('core')
makedepends=('bash'
             'binutils'
             'bison'
             'coreutils'
             'gcc'
             'glibc'
             'grep'
             'make'
             'patch')
depends=('readline')
checkdepends=('gawk')
source=("http://alpha.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        "http://www.linuxfromscratch.org/patches/downloads/${pkgname}/${pkgname}-${pkgver}-memory_leak-1.patch")

prepare(){
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgver}-memory_leak-1.patch"
}

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr             \
              --with-readline           \
              --mandir=/usr/share/man   \
              --infodir=/usr/share/info
  make
}

check(){
  cd "${pkgname}-${pkgver}"
  echo "quit" | ./bc/bc -l Test/checklib.b 2>&1 | tee test.log || true
  # This should have exactly 7 failurs or 10 failures - verify it
  num_fails=`grep "failed" test.log | grep -vc "0$"`
  if [ "${CARCH}" == "x86_64" ]
  then
    [ "${num_fails}" -eq "7" ] || exit 1
  else
    [ "${num_fails}" -eq "10" ] || exit 1
  fi
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

sha256sums=('7ee4abbcfac03d8a6e1a8a3440558a3d239d6b858585063e745c760957725ecc'
            '9e9279f1c2c4b83f3dab9672563bb02087776f04bb43eec135af117e6d4f83fb')
