# vim: set ts=2 sw=2 et:
# Maintainer:  DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=expat
pkgver=2.2.0
pkgrel=1
pkgdesc='An XML parser library'
arch=('i686' 'x86_64')
url='http://expat.sourceforge.net/'
license=('custom')
groups=('core')
depends=('glibc'
         'gcc')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2")

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --disable-static
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/usr/share/doc/expat-2.2.0"
  install -vm644 doc/*.{html,png,css} "${pkgdir}/usr/share/doc/expat-2.2.0"

  # Install custom license
  install -vDm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

sha256sums=('d9e50ff2d19b3538bd2127902a89987474e1a4db8e43a66a4d1a712ab9a504ff')

