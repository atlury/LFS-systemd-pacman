# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=lz4
pkgver=131
pkgrel=1
pkgdesc='Very fast lossless compression algorithm'
arch=('i686' 'x86_64')
url='https://github.com/Cyan4973/lz4'
license=('GPL2')
groups=('core')
checkdepends=('diffutils')
depends=('glibc'
         'gcc')
source=("lz4-r131.tar.gz::https://github.com/Cyan4973/lz4/archive/r131.tar.gz")

build() {
  cd "${pkgname}-r${pkgver}"
  make PREFIX=/usr
}

check() {
  cd "${pkgname}-r${pkgver}"
  programs/lz4 /etc/passwd passwd.lz4
  programs/lz4 -d passwd.lz4 passwd
  diff -q /etc/passwd passwd
  rm passwd
}

package() {
  cd "${pkgname}-r${pkgver}"
  make install PREFIX=/usr DESTDIR="${pkgdir}"
}

sha256sums=('9d4d00614d6b9dec3114b33d1224b6262b99ace24434c53487a0c8fd0b18cfed')
